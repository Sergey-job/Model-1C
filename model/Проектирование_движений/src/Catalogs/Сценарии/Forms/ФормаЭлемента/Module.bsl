
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПоказатьБУ = Истина;
	ПоказатьНУ = Истина;	
	ЗаполнитьПризнакФормул();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьПроверкуСценарияНаСервере();
	ЗаполнитьПризнакФормул();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВыполнитьРасчетПоФормулам();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРасчет(Команда)
	
	ВыполнитьРасчетПоФормулам();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗначенияПоказателейФормулаРасчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВвестиФормулуРасчетаВСтроке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначенияПоказателей

&НаКлиенте
Процедура ЗначенияПоказателейПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьИтогиПоВыбраннымСтрокамТаблицыПоказателиМодели();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСписокПоказателей(Команда)
	ЗаполнитьСписокПоказателейНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПроверкуСценария(Команда)
	ЗаполнитьПроверкуСценарияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокПоказателейНаСервере()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗначенияПоказателей.НомерСтроки КАК НомерСтроки,
	|	ЗначенияПоказателей.Показатель КАК Показатель,
	|	ЗначенияПоказателей.ЗначениеПоказателя КАК ЗначениеПоказателя
	|ПОМЕСТИТЬ втЗначенияПоказателей
	|ИЗ
	|	&ЗначенияПоказателей КАК ЗначенияПоказателей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСписокДвижений.Показатель КАК Показатель
	|ПОМЕСТИТЬ втПоказателиМодели
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСписокДвижений.СуммаДтБУ КАК Показатель
	|	ИЗ
	|		Справочник.МоделиДвижений.СписокДвижений КАК ТаблицаСписокДвижений
	|	ГДЕ
	|		ТаблицаСписокДвижений.Ссылка В (&МодельДвижений)
	|		И НЕ ТаблицаСписокДвижений.ЭтоГруппаДвижений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСписокДвижений.СуммаДтНУ
	|	ИЗ
	|		Справочник.МоделиДвижений.СписокДвижений КАК ТаблицаСписокДвижений
	|	ГДЕ
	|		ТаблицаСписокДвижений.Ссылка В (&МодельДвижений)
	|		И НЕ ТаблицаСписокДвижений.ЭтоГруппаДвижений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСписокДвижений.СуммаКтБУ
	|	ИЗ
	|		Справочник.МоделиДвижений.СписокДвижений КАК ТаблицаСписокДвижений
	|	ГДЕ
	|		ТаблицаСписокДвижений.Ссылка В (&МодельДвижений)
	|		И НЕ ТаблицаСписокДвижений.ЭтоГруппаДвижений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСписокДвижений.СуммаКтНУ
	|	ИЗ
	|		Справочник.МоделиДвижений.СписокДвижений КАК ТаблицаСписокДвижений
	|	ГДЕ
	|		ТаблицаСписокДвижений.Ссылка В (&МодельДвижений)
	|		И НЕ ТаблицаСписокДвижений.ЭтоГруппаДвижений) КАК ТаблицаСписокДвижений
	|ГДЕ
	|	ТаблицаСписокДвижений.Показатель <> ЗНАЧЕНИЕ(Справочник.Показатели.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПоказателиМодели.Показатель КАК Показатель,
	|	ЕСТЬNULL(втЗначенияПоказателей.ЗначениеПоказателя, 0) КАК ЗначениеПоказателя,
	|	ЕСТЬNULL(втЗначенияПоказателей.НомерСтроки, 999) КАК НомерСтроки
	|ИЗ
	|	втПоказателиМодели КАК втПоказателиМодели
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияПоказателей КАК втЗначенияПоказателей
	|		ПО (втЗначенияПоказателей.Показатель = втПоказателиМодели.Показатель)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МодельДвижений", Объект.МоделиСценария.Выгрузить().ВыгрузитьКолонку("МодельДвижений"));
	Запрос.УстановитьПараметр("ЗначенияПоказателей", Объект.ЗначенияПоказателей.Выгрузить());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Объект.ЗначенияПоказателей.Очистить();
	
	Пока Выборка.Следующий() Цикл
		ДанныеСтроки = Объект.ЗначенияПоказателей.Добавить();
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка,, "НомерСтроки");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИтогиПоВыбраннымСтрокамТаблицыПоказателиМодели()

	ИтогоЗначениеПоказателя = 0;
	Для каждого ИдентификаторСтроки Из Элементы.ЗначенияПоказателей.ВыделенныеСтроки Цикл
		ДанныеСтроки = Объект.ЗначенияПоказателей.НайтиПоИдентификатору(ИдентификаторСтроки);
		ИтогоЗначениеПоказателя = ИтогоЗначениеПоказателя + ДанныеСтроки.ЗначениеПоказателя;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПроверкуСценарияНаСервере()
	
	ВыполнитьРасчетПоФормулам();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МоделиСценария.Активно,
	|	МоделиСценария.МодельДвижений
	|ПОМЕСТИТЬ втМоделиСценария
	|ИЗ
	|	&МоделиСценария КАК МоделиСценария
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияПоказателей.Показатель,
	|	ЗначенияПоказателей.ЗначениеПоказателя
	|ПОМЕСТИТЬ втЗначенияПоказателей
	|ИЗ
	|	&ЗначенияПоказателей КАК ЗначенияПоказателей
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверкаСценария.НомерСтроки,
	|	ПроверкаСценария.Аналитика,
	|	ПроверкаСценария.Комментарий,
	|	ПроверкаСценария.СуммаДтБУ,
	|	ПроверкаСценария.СуммаДтНУ,
	|	ПроверкаСценария.СуммаКтБУ,
	|	ПроверкаСценария.СуммаКтНУ
	|ПОМЕСТИТЬ втПроверкаСценария
	|ИЗ
	|	&ПроверкаСценария КАК ПроверкаСценария
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетСценария.Аналитика КАК Аналитика,
	|	СУММА(РасчетСценария.СуммаДтБУ) КАК СуммаДтБУ,
	|	СУММА(РасчетСценария.СуммаДтНУ) КАК СуммаДтНУ,
	|	СУММА(РасчетСценария.СуммаКтБУ) КАК СуммаКтБУ,
	|	СУММА(РасчетСценария.СуммаКтНУ) КАК СуммаКтНУ,
	|	СУММА(РасчетСценария.СуммаДтБУ_Факт) КАК СуммаДтБУ_Факт,
	|	СУММА(РасчетСценария.СуммаДтНУ_Факт) КАК СуммаДтНУ_Факт,
	|	СУММА(РасчетСценария.СуммаКтБУ_Факт) КАК СуммаКтБУ_Факт,
	|	СУММА(РасчетСценария.СуммаКтНУ_Факт) КАК СуммаКтНУ_Факт
	|ИЗ
	|	(ВЫБРАТЬ
	|		втПроверкаСценария.Аналитика КАК Аналитика,
	|		втПроверкаСценария.СуммаДтБУ КАК СуммаДтБУ,
	|		втПроверкаСценария.СуммаДтНУ КАК СуммаДтНУ,
	|		втПроверкаСценария.СуммаКтБУ КАК СуммаКтБУ,
	|		втПроверкаСценария.СуммаКтНУ КАК СуммаКтНУ,
	|		0 КАК СуммаДтБУ_Факт,
	|		0 КАК СуммаДтНУ_Факт,
	|		0 КАК СуммаКтБУ_Факт,
	|		0 КАК СуммаКтНУ_Факт
	|	ИЗ
	|		втПроверкаСценария КАК втПроверкаСценария
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		МодельДвиженийСписокДвижений.АналитикаДт КАК Аналитика,
	|		0 КАК СуммаДтБУ,
	|		0 КАК СуммаДтНУ,
	|		0 КАК СуммаКтБУ,
	|		0 КАК СуммаКтНУ,
	|		ЕСТЬNULL(ТаблицаПоказателиМоделиБУ.ЗначениеПоказателя, 0) КАК СуммаДтБУ_Факт,
	|		ЕСТЬNULL(ТаблицаПоказателиМоделиНУ.ЗначениеПоказателя, 0) КАК СуммаДтНУ_Факт,
	|		0 КАК СуммаКтБУ_Факт,
	|		0 КАК СуммаКтНУ_Факт
	|	ИЗ
	|		втМоделиСценария КАК РасчетСценария
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиДвижений.СписокДвижений КАК МодельДвиженийСписокДвижений
	|			ПО МодельДвиженийСписокДвижений.Ссылка = РасчетСценария.МодельДвижений
	|			ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияПоказателей КАК ТаблицаПоказателиМоделиБУ
	|			ПО ТаблицаПоказателиМоделиБУ.Показатель = МодельДвиженийСписокДвижений.СуммаДтБУ
	|			ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияПоказателей КАК ТаблицаПоказателиМоделиНУ
	|			ПО ТаблицаПоказателиМоделиНУ.Показатель = МодельДвиженийСписокДвижений.СуммаДтНУ
	|	ГДЕ
	|		РасчетСценария.Активно
	|		И НЕ МодельДвиженийСписокДвижений.ЭтоГруппаДвижений
	|		И НЕ МодельДвиженийСписокДвижений.Отключено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		МодельДвиженийСписокДвижений.АналитикаКт,
	|		0 КАК СуммаДтБУ,
	|		0 КАК СуммаДтНУ,
	|		0 КАК СуммаКтБУ,
	|		0 КАК СуммаКтНУ,
	|		0,
	|		0,
	|		ЕСТЬNULL(ТаблицаПоказателиМоделиБУ.ЗначениеПоказателя, 0),
	|		ЕСТЬNULL(ТаблицаПоказателиМоделиНУ.ЗначениеПоказателя, 0)
	|	ИЗ
	|		втМоделиСценария КАК РасчетСценария
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиДвижений.СписокДвижений КАК МодельДвиженийСписокДвижений
	|			ПО МодельДвиженийСписокДвижений.Ссылка = РасчетСценария.МодельДвижений
	|			ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияПоказателей КАК ТаблицаПоказателиМоделиБУ
	|			ПО ТаблицаПоказателиМоделиБУ.Показатель = МодельДвиженийСписокДвижений.СуммаКтБУ
	|			ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияПоказателей КАК ТаблицаПоказателиМоделиНУ
	|			ПО ТаблицаПоказателиМоделиНУ.Показатель = МодельДвиженийСписокДвижений.СуммаКтНУ
	|	ГДЕ
	|		РасчетСценария.Активно
	|		И НЕ МодельДвиженийСписокДвижений.ЭтоГруппаДвижений
	|		И НЕ МодельДвиженийСписокДвижений.Отключено) КАК РасчетСценария
	|СГРУППИРОВАТЬ ПО
	|	РасчетСценария.Аналитика
	|УПОРЯДОЧИТЬ ПО
	|	РасчетСценария.Аналитика.Представление";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МоделиСценария", Объект.МоделиСценария.Выгрузить());
	Запрос.УстановитьПараметр("ЗначенияПоказателей", Объект.ЗначенияПоказателей.Выгрузить());
	Запрос.УстановитьПараметр("ПроверкаСценария", Объект.ПроверкаСценария.Выгрузить());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Объект.ПроверкаСценария.Очистить();
	
	Пока Выборка.Следующий() Цикл
		ДанныеСтроки = Объект.ПроверкаСценария.Добавить();
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка);
	КонецЦикла;
	

КонецПроцедуры

&НаСервере
Процедура ВыполнитьРасчетПоФормулам()

	ЗначенияПоказателей = Новый Структура;
	ИменаПоказателей = Новый Соответствие;
	Для Каждого ДанныеСтроки Из Объект.ЗначенияПоказателей Цикл
		ИмяПоказателя = "П_" + Формат(ДанныеСтроки.НомерСтроки, "ЧГ=;");
		ЗначенияПоказателей.Вставить(ИмяПоказателя, ДанныеСтроки.ЗначениеПоказателя);
		ИменаПоказателей.Вставить(Строка(ДанныеСтроки.Показатель), "ЗначенияПоказателей." + ИмяПоказателя);
	КонецЦикла;

	Для Каждого ДанныеСтроки Из Объект.ЗначенияПоказателей Цикл
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ФормулаРасчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ФормулаРасчета = ДанныеСтроки.ФормулаРасчета;
		Для Каждого КлючИЗначение Из ИменаПоказателей Цикл
			ФормулаРасчета = СтрЗаменить(ФормулаРасчета, "[" + КлючИЗначение.Ключ + "]", КлючИЗначение.Значение);
		КонецЦикла;
		
		Попытка
			ДанныеСтроки.ЗначениеПоказателя = Вычислить(ФормулаРасчета);
		Исключение
			ДанныеСтроки.ЗначениеПоказателя = 0; 
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиФормулуРасчетаВСтроке()

	ТекущиеДанные = Элементы.ЗначенияПоказателей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиФормулуРасчетаВСтрокеЗавершение", ЭтотОбъект);
	
	ПоказатьВводСтроки(
		ОписаниеОповещения, 
		ТекущиеДанные.ФормулаРасчета, 
		НСтр("ru = 'Введите формулу расчета показателя'"),, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиФормулуРасчетаВСтрокеЗавершение(ФормулаРасчета, ДопПараметры) Экспорт

	Если ФормулаРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЗначенияПоказателей.ТекущиеДанные;
	ТекущиеДанные.ФормулаРасчета = ФормулаРасчета;
	ТекущиеДанные.ЗначениеЗаданоФормулой = ЗначениеЗаполнено(ФормулаРасчета);
	ТекущиеДанные.ЗначениеПоказателя = 0;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакФормул()
	
	Для Каждого ДанныеСтроки Из Объект.ЗначенияПоказателей Цикл
		ДанныеСтроки.ЗначениеЗаданоФормулой = ЗначениеЗаполнено(ДанныеСтроки.ФормулаРасчета);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
